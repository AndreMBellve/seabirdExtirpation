to init-patches ;creating default patches and patch-sets (all values set at value for the sea)
  
  ask patches
  [
    set habitable? FALSE
    set suitable? false 
    set colony-id 0
    set habitat-attrac 0
    set occupancy 0
    set occupancy-limit 0
    set predators? false
    set pcolor blue
    set neighbourhood no-patches
  ]
  
end

to grow-islands ;creating islands and colonies
  
  let idx 1 ;initialising the round counter for definign the habitat-id variable
  
  repeat num-clust
  [
    ask one-of patches with [ count patches with [ pcolor = green ] in-radius (clust-area + 1)  = 0 ]
    [
      let cluster patches in-radius clust-area ;of patch-here;creating patch-set of the cluster
      
      set pcolor green
      set habitable? TRUE ;setting this to be desireable habitat for the turtle
      set colony-id idx ;setting habitat-id to a unique value
      
      ask n-of (count(cluster) * clust-density) cluster ;expanding the patch as a function of density and user defined area
      [
        set pcolor green
        set habitable? TRUE ;see above for this line and the one below
        set colony-id idx
      ]
    ]
    set idx (idx + 1) ;updating idx iteration to keep habitat-id names unique
  ]
  
  set colonies patches with [ colony-id > 0 ] ;the baseline initialisation for cells is 0 (i.e. sea cells are 0)
  
  repeat num-predator-islands ;will create a number of predator islands up to user defined value
  [
    ask patches with [ colony-id = (random idx + 1) and not predators? ] ;ask a random colony without predators to become predator infested
    [
      set predators? true
    ]
  ]
  
  set the-islands patches with [ habitable? ] ;convenience name
  set island-id remove-duplicates [colony-id] of the-islands
  set the-sea patches with [ not habitable? ]
  set pred-patches patches with [ predators? ]
  set safe-patches patches with [ habitable? = true and not predators? ]
  

  
  foreach island-id [n -> 
    dig-burrows n
  ]
  
  ask patches with [ habitable? ]
  [
    ifelse nhb-rad <= 1 [set neighbourhood (patch-set self neighbors with [ habitable? ])][set neighbourhood patches with [ habitable? ] in-radius nhb-rad] ; set neighbourhoods for only island patches
  ]
  
  ask the-islands
  [
    set maxK max  [ occupancy-limit ] of neighbourhood
    set habitat-attrac ( occupancy-limit / maxK ) * 0.3
  ]
  
end

to dig-burrows [ n ]
  
  let island-patches patches with [ colony-id = n ] ;looking at only this island
  let island-size count island-patches ;counting island size for determining suitability
  
  ;; seeds the grid with one patch as suitable
  ask one-of island-patches [ set suitable? true]
  let filled 1
  
  ;; this sequentially fills the grid by selecting patches and making them suitable
  while [filled <= (prop-suitable * island-size)]
  [
    ;; if rnd test is less than attract-suitable then an *unsuitable* patch next to a suitable patch is made suitable
    ifelse random-float 1 <= habitat-aggregation
    [
      ask one-of [ neighbors with [ habitable? ] ] of (island-patches with [suitable?])
      [
        if suitable? = false
        [
          set suitable? true
          set filled filled + 1
        ]
      ]
    ]
    
    ;; otherwise pick a patch at random (this could ne neighbouring a suitable patch)
    ;; if you want to make sure it does not you'd need ot use
    ;; ask one-of patches with [(not suitable?) and (count neighbors with [suitable?] = 0)]
    [
      ask one-of patches with [habitable? and not suitable?]
      ;;ask one-of patches with [(not suitable?) and (count neighbors with [suitable?] = 0)]
      [
        set suitable? true
        set filled filled + 1
      ]
    ]
 ]
  
  ;Now set two different poisson distributions
  ask island-patches 
  [
    ifelse suitable? 
    [
      set occupancy-limit random-poisson high-lambda
    ]
    [
      set occupancy-limit random-poisson low-lambda
    ]
  ]
  
  
  ;Now diffuse surface
  diffuse occupancy-limit diffusion-prop
  
  ask patches
  [
    set occupancy-limit floor occupancy-limit
  ]
  
  ;Removing occupancy limits that shouldn't exist
  ask the-sea 
  [
    set occupancy-limit 0
  ]
  
 
  let min-occ-lim min [ occupancy-limit ] of island-patches
  let max-occ-lim max [ occupancy-limit ] of island-patches
  
 ask island-patches
  [ 
    set pcolor scale-color green occupancy-limit max-occ-lim min-occ-lim
  ]
  
  
end

to init-adults
  
  ;initialising males/females based on sex-ratios
  create-males starting-seabird-pop * sex-ratio ;assuming even sex-ratios to begin with
  create-females starting-seabird-pop * (1 - sex-ratio)
  
  ;initialising basic parameters of birds
  ask turtles
  [
    set shape "bird side"
    setxy 0 0 ;making them all start on the left edge of the ma
    set age age-at-first-breeding + random-poisson 2 ;adding some age variability
    set settled? false ;Has not been assigned a colony
    set breeding? false ;Has not found a patch
    set breeding-ground-id 0
    set breeding-grounds no-patches
    set mating? false ;Has not found a mate
    set mating-id -1
    set last-breeding-success? true
    set size 2
    set life-stage "Adult"
    set mate no-turtles
    set burrow no-patches
  ]
  
  ask females
  [
    set color orange
  ]
  
  ask males
  [
    set color violet
  ]
  
  set breeders turtles with [ life-stage = "Adult"]
  
end

to assign-colonies
  
  if debug? [ show island-id ]
  
  ask breeders with [not settled?]
  [
    ;set breeding-ground-id to match a colony-id and that the occupancy is less than the occupancy-limit
    let target-grd one-of island-id
    set breeding-ground-id target-grd
    set natal-ground-id target-grd ;intialising as the breeding-grounds = natal grounds
    set settled? true
    set breeding-grounds patch-set patches with [ colony-id = target-grd ]
    ; if [debug?] [ show "target grd id: " show target-grd ]  
  ]
  
end

to assign-mates
  ;initialising mating pairs  
  foreach  island-id  [n ->  
    while [ (count females with [ life-stage = "Adult" and breeding-ground-id = n and mating-id = -1 ]  > 0 ) and count( males with [ life-stage = "Adult" and breeding-ground-id = n and mating-id = -1 ] )  > 0]
    [
      if debug?
      [
        show n
      ]
      
      let free-females (turtle-set females with [ mating-id = -1 and life-stage = "Adult" and breeding-ground-id = n ] )
      if any? free-females
      [
        
        if debug?
        [
          show count(free-females)
        ]
        
        let f-mate one-of free-females
        let free-males (turtle-set males with [ mating-id = -1 and life-stage = "Adult" and breeding-ground-id = n ])
        
        if any? free-males
        [       
          if debug?
          [
            show count(free-males)
          ]
          
          let m-mate one-of free-males
          ask f-mate 
          [ 
            if debug?
            [
              show [ who ] of m-mate ;; GP mate -> m-mate
            ]
            
            set mate m-mate
            set mating-id [ who ] of m-mate
          ]
          
          ask m-mate 
          [ 
            if debug?
            [
              show [ who ] of f-mate 
            ]
            
            set mate f-mate
            set mating? true 
          ]
        ]
      ]
    ]
  ]
end

to assign-burrows
  
  ask males with [ life-stage = "Adult" and breeding-grounds != no-patches ]
  [
    set burrow one-of breeding-grounds with [ occupancy < occupancy-limit ]
    if debug? [ show burrow ] 
  ]
  
end

to init-juveniles
  
  create-males starting-juveniles / 2
  [
    raise-chick violet
  ]
  
  create-females starting-juveniles / 2
  [

    raise-chick orange
  ]
  
  
  ask females
  [
    set color orange
  ]
  
  ask males
  [
    set color violet 
  ]
  
  set recruits turtles with [ life-stage = "Juvenile"]
  
end


to raise-chick [ colour ]
  
  set color colour
  
  set size 3
  set shape "bird side"
  set age 0
  set settled? false
  set breeding? false
  set mating? false
  set last-breeding-success? false
  set life-stage "Juvenile"
  set natal-ground-id colony-id
  set breeding-ground-id 0
  set breeding-grounds no-patches
  set mating-id -1
  set mate no-turtles
  set burrow no-patches
  
end


to assign-natal-grounds
  ask recruits
  [ 
    set natal-ground-id one-of island-id ;Assigning a random colony as natal grounds
  ]
end



